# 0201. 提取块里的额属性值

目前已经迭代了好几个版本，详见数据流开发的 git 仓库。（2020-11-22）

## 01. 参考

[Forums - AutoCAD Forums](https://www.cadtutor.net/forum/)

### 01

[SSGET - BLOCK, then Search Descriptions - Page 2 - AutoLISP, Visual LISP & DCL - AutoCAD Forums](https://www.cadtutor.net/forum/topic/64986-ssget-block-then-search-descriptions/page/2/#comments)

```
(defun c:test (/ _getattvalue s v)
 ;; RJP - Simple get attribute value sub .. no error checking
 (defun _getattvalue (block tag)
   (vl-some
     '(lambda (att)
    (cond ((eq (strcase tag) (strcase (vla-get-tagstring att))) (vla-get-textstring att)))
      )
     (vlax-invoke block 'getattributes)
   )
 )
 ;; RJP - added (66 . 1) to filter ( attributed blocks )
 (cond    ((setq
      s (ssget "_C" '(7.244 2.071) '(16.665 10.003) '((0 . "INSERT") (8 . "FTG-Iso") (66 . 1)))
    )
    (foreach en (vl-remove-if 'listp (mapcar 'cadr (ssnamex s)))
      (if (and ;; If we have a value, and it does not match the filter then remove item from selection
           (setq v (_getattvalue (vlax-ename->vla-object en) "Description"))
           ;; Wcmatch example
           ;; (not (wcmatch (strcase v) "*SKSW*,*NISC*,*NIPL*,*THSC*,*WTBW*,*NRSC*,*PL*,*KASC*,*KASW*,"))
           ;; vl-string-search example ( more legible IMO )
           (not (vl-some '(lambda (x) (vl-string-search x (strcase v)))
                 '("SKSW" "NISC" "NIPL" "THSC" "WTBW" "NRSC" "PL" "KASC" "KASW")
            )
           )
          )
        (ssdel en s)
      )
    )
    ;; Highlight selection
    (sssetfirst nil s)
   )
 )
 (princ)
)
(vl-load-com)
```

If you don't need the selection set, it's more logical to just change the layer of the items within the loop.

```
(if (and ;; If we have a value, and it does not match the filter then remove item from selection
	    (setq v (_getattvalue (vlax-ename->vla-object en) "Description"))
	    ;; vl-string-search example ( more legible IMO )
	    (vl-some '(lambda (x) (vl-string-search x (strcase v)))
		     '("SKSW" "NISC" "NIPL" "THSC" "WTBW" "NRSC" "PL" "KASC" "KASW")
	    )
       )
     (entmod (append (entget en) '((8 . "NewLayer"))))
   )
```

### 02

[Using ssget "X" to Get block name if block contain more then 20 lines of attribu](https://www.theswamp.org/index.php?topic=38912.0)

The ssget function can only search the drawing database for primary entities, not subentities such as attributes/vertices.

Therefore, you have two options: the first would be to iterate through all attributed blocks in the drawing, then single out those that have more than 20 attributes:

```
(defun GetBlocksWithMoreThan20Attribs ( / at en i ss )
  (if (setq ss (ssget "_X" '((0 . "INSERT") (66 . 1))))
    (repeat (setq i (sslength ss))
      (setq en (ssname ss (setq i (1- i)))
            at 0
      )
      (while (eq "ATTRIB" (cdr (assoc 0 (entget (setq en (entnext en))))))
        (setq at (1+ at))
      )
      (if (< at 20)
        (ssdel (cdr (assoc 330 (entget en))) ss)
      )
    )
  )
  ss
)
```

1『

上面的代码很有借鉴意义。

```
// 获取块名为 blockname 的块集合，发现块名是中文不行
(setq ss (ssget "X" '((0 . "insert") (2 . "blockname"))))
// 不要选项 "X" 表示手动框选
(setq ss (ssget '((0 . "insert") (2 . "blockname"))))

// 获取所有的块集合
(setq ss (ssget "_X" '((0 . "INSERT") (66 . 1))))

(sslength ss)
```

』

The second would be to iterate through the block table, and collect the names of blocks with more than 20 attribute definitions, then create a selection set of these blocks.

```
(defun GetBlocksWithMoreThan20AttDefs ( / at bd be bl nm )
  (while (setq bd (tblnext "BLOCK" (null bd)))
    (if (= 2 (logand 2 (cdr (assoc 70 bd))))
      (progn
        (setq at 0
              nm (cdr (assoc 2 bd))
              be (tblobjname "BLOCK" nm)
        )
        (while (setq be (entnext be))
          (if (eq "ATTDEF" (cdr (assoc 0 (entget be))))
            (setq at (1+ at))
          )
        )
        (if (<= 20 at)
          (setq bl (cons "," (cons nm bl)))
        )
      )
    )
  )
  (ssget "_X" (list '(0 . "INSERT") '(66 . 1) (cons 2 (apply 'strcat (cdr bl)))))
)
```

The second method will fail however if attributes have been added to the block outside of the block definition, but this is an unlikely case. When entmake'ing a Block Reference (Insert), attributes which do not belong to the Block Definition can be programmatically added to the Insert entity using entmake.

### 03

[Autolisp to Update Titleblock Attributes](https://gist.github.com/rudderdon/d8880c26253634b1a2908c14726f3367)

```
(defun c:magic ()
  (setq osm (getvar "OSMODE"))
  (setq attq (getvar "ATTREQ"))
  (setvar "OSMODE" 0)
  (setvar "ATTREQ" 0)
  (command "-xref" "p" "TbInfo-CP51" "..\\..\\Blks\\TbInfo.dwg")
  (doRevs)
  (doSheet)
  (command "-layer" "thaw" "*|E-REVS-SIGN-1" "on" "*|E-REVS-SIGN-1" "")

  (setvar "OSMODE" osm)
  (setvar "ATTREQ" attq)
)

(defun doRevs ()
  (setq ss (ssget "x" '((0 . "INSERT") (2 . "ATTRIBUTE-BLOCK-NAME"))))
  (setq i 0)
  (if (/= ss nil)
    (progn
      (if (/= nil (ssname ss i))
	(progn
	  (setq ent (entget (ssname ss i)))
	  (setq p1 (cdr (assoc 10 ent)))
	  (setq blk (ssname ss i))
	  (setq entx (entget (entnext (cdr (assoc -1 ent)))))
	  (while (= "ATTRIB" (cdr (assoc 0 entx)))
	    (setq value (cdr (assoc 2 entx)))
	    (if	(= value "REV-NO-1")
	      (progn
		(setq a (cons 1 "0"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "REV-NO-1-DATE")
	      (progn
		(setq a (cons 1 "17.03.10"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "REV-1-DESCRIPTION")
	      (progn
		(setq a (cons 1 "ISSUED FOR XXXXXXXXXXXXXXXXXX"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "REV-NO-2")
	      (progn
		(setq a (cons 1 "1"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "REV-NO-2-DATE")
	      (progn
		(setq a (cons 1 "15.09.10"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "REV-2-DESCRIPTION")
	      (progn
		(setq a (cons 1 "ISSUED FOR XXXXXXXXXXXXXXXXX"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    ****************************************************************************************
	    (setq entx (entget (entnext (cdr (assoc -1 entx)))))
	  )
	  (entupd blk)
	  (setq i (+ 1 i))
	)
      )
      (prompt "Attribute updated successfully. ")
    )
  )
  (princ)
)

(defun doSheet ()

  (setq ss (ssget "x" '((0 . "INSERT") (2 . "BLOCK NAME"))))
  (setq i 0)
  (if (/= ss nil)
    (progn
      (if (/= nil (ssname ss i))
	(progn
	  (setq ent (entget (ssname ss i)))
	  (setq p1 (cdr (assoc 10 ent)))
	  (setq blk (ssname ss i))
	  (setq entx (entget (entnext (cdr (assoc -1 ent)))))
	  (while (= "ATTRIB" (cdr (assoc 0 entx)))
	    (setq value (cdr (assoc 2 entx)))
	    (if	(= value "REV-NO")
	      (progn
		(setq a (cons 1 "1"))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )
	    (if	(= value "PROJECT-JOB-NO")
	      (progn
		(setq a (cons 1 ""))
		(setq b (assoc 1 entx))
		(entmod (subst a b entx))
	      )
	    )

	    ****************************************************************************************
	    (setq entx (entget (entnext (cdr (assoc -1 entx)))))
	  )
	  (entupd blk)
	  (setq i (+ 1 i))
	)
      )
      (prompt "Attribute updated successfully. ")
    )
  )
  (princ)
)
```
