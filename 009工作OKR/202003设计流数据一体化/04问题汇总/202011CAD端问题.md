## 01. 开发自动生成带属性块时遇到的问题

2020-11-20

牢记一个核心原则，先手动生成想要的带属性块。通过 `entget` 获取图形以及「属性」的数据集，模拟这些数据集，将模拟的数据集传递给 `entmake` 函数，无法生成的话说明你模拟的数据集里有一些没必要的数据。试验成功后可以借鉴单元测试的思想，一点点简化「数据集」来精简，一发现无法生成的话就放弃修改。

自己开始按照这个思路走，就是因为自己写属性的提示项数据 `(cons 3 propertySpecify)` 导致生成块的时候报错 `无效的复杂对象`，然后就不坚定了，放弃的上面的核心原则，只是通过简单的「数据集」企图生成，均已失败告终。最终还是采用上面的思路成功后再一步步简化「数据集」。

```c
(defun GenerateBlockReference (insPt blockName blockLayer /) 
  (entmake 
    (list (cons 0 "INSERT") (cons 100 "AcDbEntity") (cons 67 0) (cons 410 "Model") (cons 8 blockLayer) (cons 100 "AcDbBlockReference") 
          (cons 66 1) (cons 2 blockName) (cons 10 insPt) (cons 41 1.0) (cons 42 1.0) (cons 43 1.0) (cons 50 0.0) (cons 70 0) (cons 71 0) 
          (cons 44 0.0) (cons 45 0.0) (cons 210 '(0.0 0.0 1.0))
    )
  ) 
  (princ)
)

(defun GenerateBlockAttribute (insPt propertyName blockLayer /)
  (entmake 
    (list (cons 0 "ATTRIB") (cons 100 "AcDbEntity") (cons 67 0) (cons 410 "Model") (cons 8 blockLayer) (cons 100 "AcDbText") 
          (cons 10 insPt) (cons 40 3.0) (cons 1 "") (cons 50 0.0) (cons 41 0.7) (cons 51 0.0) (cons  7 "Standard") (cons 71 0) (cons 72 0) 
          (cons 11 '(0.0 0.0 0.0)) (cons 210 '(0.0 0.0 1.0)) (cons 100 "AcDbAttribute") (cons 280 0) (cons 2 propertyName) (cons 70 0) 
          (cons 73 0) (cons 74 0) (cons 280 0)
    )
  )
  (princ)
)

(defun GeneratejoinDrawArrowTo ()
  (setq insPt (getpoint "\n选取接图箭头的插入点："))
  (GenerateBlockReference insPt "JoinDrawArrowTo" "接图箭头")
  (GenerateBlockAttribute (MoveInsertPosition insPt 1 4) "FROMTO" "接图箭头")
  (GenerateBlockAttribute (MoveInsertPosition insPt 1 -1.5) "DRAWNUM" "接图箭头")
  (entmake 
    (list (cons 0 "SEQEND") (cons 100 "AcDbEntity"))
  )
  (princ)
)
```